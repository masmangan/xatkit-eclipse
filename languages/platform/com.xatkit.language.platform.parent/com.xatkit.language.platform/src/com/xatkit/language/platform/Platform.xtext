grammar com.xatkit.language.platform.Platform with com.xatkit.language.intent.Intent

import "com.xatkit.platform" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "com.xatkit.intent" as intent
import "com.xatkit.common" as common
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

Platform returns PlatformDefinition:
	{PlatformDefinition}
	(imports+=PlatformImportDeclaration)*
	(abstract?='Abstract')?
	'Platform' name=EString
	('extends' extends=[PlatformDefinition|ID])?
	('path' runtimePath=EString)?
	('providers' '{'
		eventProviderDefinitions+=(InputProviderDefinition|EventProviderDefinition)
		(eventProviderDefinitions+=(InputProviderDefinition|EventProviderDefinition))*
	'}')?
	('actions' '{' 
		actions+=Action 
		(actions+=Action)* 
	'}')?
	;
	
PlatformImportDeclaration returns common::PlatformImportDeclaration:
	{common::PlatformImportDeclaration} 'import platform' path=STRING ('as' alias=ID)?
;	

EventProviderDefinition returns EventProviderDefinition:
	{EventProviderDefinition}
	'event' name=EString
	'{'
		(eventDefinitions+=EventDefinition)*
		(outContexts+=OutContextDefinition)*
		
	'}'
;

InputProviderDefinition returns InputProviderDefinition:
	{InputProviderDefinition}
	'input' name=EString 
	(
	'{'
	  (outContexts+=OutContextDefinition)*
	'}' 	
	)?
;

/**
 * Return type is optional to ease support for previous platform versions. Object is inferred if there is no return type.
 */
Action returns ActionDefinition:
	{ActionDefinition}
	name=EString ('('parameters+=Parameter (',' parameters+=Parameter)*')')?
	(':'
	returnType=JvmTypeReference)?
;


/**
 * Parameter type is optional to ease support for previous platform versions. Object is inferred if there is no type.
 */
Parameter returns Parameter:
	{Parameter}
	key=EString
	(':'
	type= JvmTypeReference)?
;

EString returns ecore::EString:
	STRING | ID('->'ID)*;
